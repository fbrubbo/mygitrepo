apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'

group = 'br.com.datamaio'
sourceCompatibility = 1.8
targetCompatibility = 1.8
version = '1.0'

configurations {
	excludedDeps
}

//Configura o projeto para utilizar o projeto ao invÃ©s do jar
gradle.taskGraph.whenReady {taskGraph ->
	 eclipse {
		 def eclipseWSProjectNames = [] as java.util.Set;
		 def eclipseWSDirs =  file("../").list();

		 configurations.all.each{ configuration ->
			 configuration.dependencies.each{ dep ->
				 eclipseWSDirs.each {projectName ->
					 if( projectName.equalsIgnoreCase(dep.name) ){
						 eclipseWSProjectNames.add(projectName);
						 configurations.excludedDeps.dependencies.add(dep);
					 }
				 }
			 }
		 }

		 classpath {
			 file{
				 withXml { xml ->
					 def node = xml.asNode()

					 eclipseWSProjectNames.collect{ projectName ->
						 println  "\tThe project " + projectName + " was found in the workspace. Using it instead of its JAR dependency"
						 node.appendNode( 'classpathentry', [ kind: 'src', path:"/${projectName}", exported: 'true', combineaccessrules:'false'])
					 }
				 }
			 }

			 if(configurations.excludedDeps.dependencies.size()>0){
				 minusConfigurations += [configurations.excludedDeps];
			 }
		 }
	 }
}

jar {
    manifest {
        attributes 'Implementation-Title': 'EnvConfigurator', 'Implementation-Version': version
    }
}

repositories {
    mavenCentral()
	maven {
		url "file:///opt/gradle-repo/maven-repo/"
	}
}

dependencies {
    compile 'br.com.datamaio:UtilityFwk:1+'
	compile 'log4j:log4j:1.2.+'
	compile 'org.jasypt:jasypt:1.9.0'
	compile 'org.codehaus.groovy:groovy-all:2.3.6'
	
    testCompile 'junit:junit:4.+'
}

uploadArchives {
	repositories {
		mavenDeployer {
			repository(url: "file:///opt/gradle-repo/maven-repo/")
		}
	}
}

task wrapper (type: Wrapper) {
    gradleVersion = '2.1-rc-2'
}

